# Maintainer: Daichi Shinozaki <dsdseg@gmail.com>
# Contributor: Valentin V. Bartenev <ne@vbart.ru> http://vbart.info/

pkgname=ctpp2-fmi
pkgver=2.8.9
pkgrel=1
pkgdesc='CTPP (or CT++) is the fastest template engine which completely written in C++'
arch=(i686 x86_64)
url='http://ctpp.havoc.ru/en/'
optdepends=(
  'iconv: iconv support'
  'md5: md5 support'
)
makedepends=(cmake make)
source=("git+https://github.com/fmidev/ctpp2.git"
        'CTPP2FileSourceLoader.patch')
license=('custom:BSD-like')
md5sums=('SKIP'
         'ed067af369dd96ce80647071081eeefd')
sha256sums=('SKIP'
            '93a23e1e5ae294c009a5457116a6b40d4af64c2b1ef6bdcdf2d80e272012cbc4')

pkgver() {
  grep ^Version: ctpp2/ctpp2.spec | awk '{print $2}'
}

build() {
  cd "$srcdir/ctpp2"
  patch -p0 -i $srcdir/CTPP2FileSourceLoader.patch  
  if [ ! -d build ]; then 
    mkdir build
  fi
  cd build
  cmake -Wno-dev -DCMAKE_INSTALL_PREFIX=/usr -DSKIP_RELINK_RPATH:BOOL=ON ..
  make VERBOSE=1
}

#check() {
## NOTE: test 26 fail (2.8.2, 2.8.3)
#  cd "ctpp2/build"
#  env LD_LIBRARY_PATH=".:$LD_LIBRARY_PATH" make test
#}

package() {
  cd "$srcdir/ctpp2"/build
  make DESTDIR="$pkgdir" install
  mkdir -p $pkgdir/usr/share
  mv $pkgdir/usr/man $pkgdir/usr/share/
  install -Dm 644 ../LICENSE "$pkgdir/usr/share/licenses/ctpp2/LICENSE"
}

# Enjoy! ;)
